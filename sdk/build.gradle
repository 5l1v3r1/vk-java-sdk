apply plugin: 'maven'
apply plugin: 'signing'

ext {
    log4jVersion = '2.5'
    gsonVersion = '2.6.2'
    commonsLangVersion = '3.4'
    commonsIOVersion = '2.4'
    commonsCLIVersion = '1.3.1'
    commonsCollectionsVersion = '4.1'
    httpCoreVersion = '4.4.4'
    httpClientVersion = '4.5.2'
    testngVersion = '6.9.10'
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
    compile group: 'org.apache.commons', name: 'commons-collections4', version: commonsCollectionsVersion
    compile group: 'commons-io', name: 'commons-io', version: commonsIOVersion
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: httpCoreVersion
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: httpClientVersion
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: httpClientVersion

    testCompile group: 'org.testng', name: 'testng', version: testngVersion
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Java SDK for VK API'
                packaging 'jar'

                description 'Java library for VK API interaction, includes OAuth 2.0 authorization and API methods.'
                url 'https://github.com/VKCOM/vk-java-sdk'

                scm {
                    connection 'scm:git@github.com:VKCOM/vk-java-sdk.git'
                    developerConnection 'scm:git@github.com:VKCOM/vk-java-sdk.git'
                    url 'scm:git@github.com:VKCOM/vk-java-sdk.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tsivarev'
                        name 'Anton Tsivarev'
                        email 'a.tsivarev@corp.vk.com'
                    }
                }
            }
        }
    }
}